#!/usr/bin/env zsh

SSH_HOST_PLUGIN_DIR="${0:A:h:h}"
source "${SSH_HOST_PLUGIN_DIR}/ssh-host.plugin.conf"
source "${SSH_HOST_PLUGIN_DIR}/utils/system.zsh"
source "${SSH_HOST_PLUGIN_DIR}/utils/host.zsh"
source "${SSH_HOST_PLUGIN_DIR}/utils/fzf.zsh"
source "${SSH_HOST_PLUGIN_DIR}/utils/validation.zsh"
source "${SSH_HOST_PLUGIN_DIR}/utils/key.zsh"

_ssh_host_add() {
    local host_alias hostname user port key_type
    input() {
        local prompt="$1" placeholder="${2}" options="$3" validator="$4"
        _ssh_host_fzf_input "$validator" \
                            "$options" \
                            --prompt="$prompt" \
                            --ghost="$placeholder" \
                            --info="inline-right: New Host Setup " \
                            --footer=" [Enter] Submit • [Esc] Quit " \
                            --preview="${SSH_HOST_PLUGIN_DIR}/src/_ssh_host_preview '${host_alias:-"..."}' '${hostname:-"..."}' '${user:-"..."}' '${port:-"..."}' '${key_type:-"..."}'"
    }

    host_alias=$(input  "Enter unique host alias name: " \
                        "api-prod-eu1" \
                        "" \
                        "_ssh_host_validate_alias") || return 1

    hostname=$(input  "Enter the server hostname or IP address: " \
                      "api1.eu.prod.example.com" \
                      "" \
                      "_ssh_host_validate_hostname") || return 1

    user=$(input  "Enter the SSH username: " \
                  "ubuntu" \
                  "" \
                  "_ssh_host_validate_username") || return 1

    port=$(input  "Select port or type custom port: " \
                  "2202" \
                  "22\n2222\n2200\n8022\n22022" \
                  "_ssh_host_validate_port") || return 1

    key_type=$(input  "Choose SSH key type: " \
                      "ed25519" \
                      "ed25519 - recommended NIST\nrsa - legacy rsa-4096" \
                      "_ssh_host_validate_key_type") || return 1

    confirm_result=$(input  "Confirm: " \
                            "" \
                            "Yes - Create this configuration\nNo - Cancel" \
                            "") || return 1

    [[ "$confirm_result" == *"Yes"* ]] || { echo "Configuration cancelled"; return 1; }
    echo "Adding configuration ..."
    echo "'${host_alias}' '${hostname}' '${user}' '${port}' '${key_type}'"

    key_file=$(_ssh_host_generate_key "$host_alias" "$key_type")
    _ssh_host_add_to_config "$host_alias" "$hostname" "$user" "$port" "$key_file"
    _ssh_host_add_to_agent "$key_file"

    cat <<EOF

Host '$host_alias' successfully configured!

Next Steps:
────────────────────────────────────────────────
1. Deploy your public key to the server manually

Public key content:
   $(cat "${key_file}.pub")

2. Test the connection:
   ssh $host_alias

3. Your public key is located at:
   ${key_file}.pub

EOF
}

[[ $# -gt 0 ]] && _ssh_host_add "$@"
